# Multi-Projects

## Advantages

1. Clear isolation between the modules
2. Code Reuse
3. More efficient build & testing.

## Getting Started

1. So the structure of a multimodule gradle project is like this `gradle-multiProject-initial`.

```bash
sbala@sbala-Nitro-AN515-52 /media/sbala/Data/CodeSource/gradle-projects/gradle-multiProject/gradle-multiProject-initial 
$ tree -L 3
.
└── allAboutNumbers-initial
    ├── build.gradle
    ├── pickerNumberApplication1-initial
    │   ├── bin
    │   ├── build.gradle
    │   └── src
    ├── plusplus-initial
    │   ├── bin
    │   ├── build.gradle
    │   └── src
    ├── randomNumberGen1-initial
    │   ├── bin
    │   ├── build.gradle
    │   └── src
    └── settings.gradle
```

2. So each module has a `build.gradle` & a we also see a `build.gradle` in the global scope alongside a `settings.gradle`
3. The `settings.gradle` is mandatory in multi-module gradle apps.
4. Its critical as it helps us configure the instantiation of all the sub-project objects.

File - `gradle-multiProject-initial/allAboutNumbers-initial/settings.gradle`

```gradle
include 'pickerNumberApplication1-initial', 'plusplus-initial', 'randomNumberGen1-initial'
rootProject.name = 'allAboutNumbers-initial'
```

5. Now back to the global `build.gradle` file - `gradle-multiProject-initial/allAboutNumbers-initial/build.gradle`.

```gradle
subprojects {
    apply plugin: 'java'
    group = 'com.denofprogramming.com'
    version = '0.0.1-SNAPSHOT'
    
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
    
    repositories {
    	mavenCentral()
    }
}
```


6. We utilize a tag - subprojects { } which accepts a closure. This applies to each of the sub-projects. So the `apply plugin: 'java'` is applied to all sub-projects with all the other mentioned values.

7. Lets add Dependecies for a module - `projects ('module-name'){ }`. Which takes a path and returns the project object for the path we provide. So in short we can update dependencies or projects for a module through the global `build.gradle` file itself

File - `gradle-multiProject-initial/allAboutNumbers-initial/build.gradle`

```gradle
subprojects {
    apply plugin: 'java'
    group = 'com.denofprogramming.com'
    version = '0.0.1-SNAPSHOT'    
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17    
    repositories {
    	mavenCentral()
    }
}
project ( ':pickerNumberApplication1-initial' ) {
	dependencies {
		implementation 'org.apache.commons:commons-math3:3.2'
        implementation 'org.apache.logging.log4j:log4j-core:2.5'
        testImplementation  "junit:junit:4.12"
	}
} 
project ( ':plusplus-initial' ) {
	dependencies {
        // Essentially conveys this module is dependent on randomNumberGen1-initial module
		implementation project(':randomNumberGen1-initial')
	}	
} 
project ( ':randomNumberGen1-initial' ) {	
	dependencies {
		implementation 'org.apache.commons:commons-math3:3.2'
        implementation 'org.apache.logging.log4j:log4j-core:2.5'
        testImplementation  "junit:junit:4.12"
	}
} 
```

8. The above code should resolve the dependency issue of each sub-module without us update each modules `build.gradle` file.

9. However if we try to build the project it fails. Because even though we resolved the dependencies. However the transitive dependency is not available during build & also we need to define an `api` based scope.

10. So we have to provide a library scope to the module - `randomNumberGen1-initial` . Once we do this , we utilize it as library-api-scope in other projects.

File - `gradle-multiProject-initial/allAboutNumbers-initial/build.gradle`

```gradle
project ( ':plusplus-initial' ) {
	dependencies {
		implementation project(':randomNumberGen1-initial')
	}
} 

project ( ':randomNumberGen1-initial' ) {
	apply plugin: 'java-library'	
	dependencies {
		api 'org.apache.commons:commons-math3:3.2'
        implementation 'org.apache.logging.log4j:log4j-core:2.5'
        testImplementation  "junit:junit:4.12"
	}
} 
```


# Gradle Task Dependencies

## Tasks Sequence

1. Suppose we have multiple tasks , how do we execute them ?
2. Well we can extend the `defaultTasks`

```gradle
defaultTasks 'step1','step2','step3','step4'
task step1{
	doLast{
		logger.info " >> build.gradle >> Task $name invoked"
	}
}
task step2{
	doLast{
		logger.info " >> build.gradle >> Task $name invoked"
	}
}
task step3{
	doLast{
		logger.info " >> build.gradle >> Task $name invoked"
	}
}
task step4{
	doLast{
		logger.info " >> build.gradle >> Task $name invoked"
	}
}
```

Output :

```
Tasks to be executed: [task ':step1', task ':step2', task ':step3', task ':step4']
> Task :step1
 >> build.gradle >> Task step1 invoked
> Task :step2
 >> build.gradle >> Task step2 invoked
> Task :step3
 >> build.gradle >> Task step3 invoked
> Task :step4
 >> build.gradle >> Task step4 invoked
```

3. So above we run all the tasks & also see a list of the task. Now this is good but we are hardcoding it. Its not elegant. Lets use a more flexible way. `Gradle allows us to have our tasks depend on other tasks so we can build up a dependency graph of tasks and then gradle works out during the configuration phase which tasks to execute based on the graph order`. One of the many benefits of having a configuration phase.

4. Simple Example using `dependsOn`

```gradle
defaultTasks 'step4'
task step3{
	doLast{
		logger.info " >> build.gradle >> Task $name invoked"
	}
}
task step4 (dependsOn: 'step3' ){
	doLast{
		logger.info " >> build.gradle >> Task $name invoked"
	}
}
```

Output : 

```
Tasks to be executed: [task ':step3', task ':step4']
> Task :step3
 >> build.gradle >> Task step3 invoked
> Task :step4
 >> build.gradle >> Task step4 invoked
```

5. We can extend the `dependsOn` logic to a number of tasks. Such as `task step4 (dependsOn: ['step3','step2'] ){` & this effects the execution order - `Tasks to be executed: [task ':step2', task ':step3', task ':step4']`.

6. What happens when 2 tasks A & B both have a dependency on task C. Does 'C' run twice ? - Nope. Gradle is smart. We can observe this is the tasks graph

7. Taking it a stuff further we can also use a closure instead of hardcoded names used in `dependsOn`. The result of the closure should be single or multiple task objects otherwise we will see an error.

8. Its thanks to `getTasks()` present in `Interface Project` which returns a `Interface TaskContainer`. Within the `Interface TaskContainer` we have a method called `findAll { }` which returns the collection of objects.

9. To get access to `findAll()`. We can use `project.tasks.findAll { }`
10. 

```gradle
task step2{
	doLast{
		logger.info " >> build.gradle >> Task $name invoked"
	}
}
logger.info " >> build.gradle >> ${ project.tasks.findAll { task -> task.name.startsWith('step2') } }"	
```

Output :
```
> Configure project :
 >> build.gradle >> [task ':step2']
```

11. We can then extend this further.

```gradle
defaultTasks 'step4'
task step2{
	doLast{
		logger.info " >> build.gradle >> Task $name invoked"
	}
}
task step3{
	doLast{
		logger.info " >> build.gradle >> Task $name invoked"
	}
}
task step4 (dependsOn: ['step3',project.tasks.findAll{task->task.name.startsWith('step2')}]){
	doLast{
		logger.info " >> build.gradle >> Task $name invoked"
	}
}
```

Output 

```
Tasks to be executed: [task ':step2', task ':step3', task ':step4']
```

12. In the above , we could have very well mentioned just `step` instead of `step2` and then this would have fired all the tasks.

13. DependsOn with . notation

```gradle
defaultTasks 'step4'
task step1{
	doLast{
		logger.info " >> build.gradle >> Task $name invoked"
	}
}
task step2{
	doLast{
		logger.info " >> build.gradle >> Task $name invoked"
	}
}
task step4{
	doLast{
		logger.info " >> build.gradle >> Task $name invoked"
	}
}
step4.dependsOn step1,step2
// We can also do
// step4.dependsOn step1,tasks.findAll{task->task.name.startsWith('step2')}
// We can also do the below check operation as well

if(Math.random() < 0.5) {
	step4.dependsOn step1,step2
}
```

Output

```
Tasks to be executed: [task ':step1', task ':step2', task ':step4']
```

## Task Dependency Graph - Direct Acyclic Graph

1. The graph lives in the gradle domain object. We can access it using. We have been seeing it getting logged in the console for some time now. Ex : `Tasks to be executed: [task ':step1', task ':step2', task ':step4']`
2. Its recommended to access the task graph under the `whenready` method as the graph is still bulding in the cofig phase

3. Lets access and print the task graph

```gradle
defaultTasks 'step4'
task step1{
	doLast{
		logger.info " >> build.gradle >> Task $name invoked"
	}
}
task step2{
	doLast{
		logger.info " >> build.gradle >> Task $name invoked"
	}
}
task step4{
	doLast{
		logger.info " >> build.gradle >> Task $name invoked"
	}
}
step4.dependsOn step1,step2
project.gradle.taskGraph.whenReady { 
	logger.info " >> build.gradle >> Task Graph : $project.gradle.taskGraph.allTasks"
}
```

Output :

```
 >> build.gradle >> Task Graph : [task ':step1', task ':step2', task ':step4']
```

4. Now we can use if-else for a condition based  scenario

```gradle
defaultTasks 'step4'
task step1{
	doLast{
		logger.info " >> build.gradle >> Task $name invoked"
	}
}
task step2{
	doLast{
		logger.info " >> build.gradle >> Task $name invoked"
	}
}
task step4{
	doLast{
		logger.info " >> build.gradle >> Task $name invoked for Version : $version"
	}
}
if ( Math.random() < 0.5 ) {
	step4.dependsOn step1,step2
}
project.gradle.taskGraph.whenReady { taskGraph -> 
	if ( taskGraph.hasTask ( step2 ) ) {
		project.version = '1.0'
	}else{
		project.version = '0.1-Snapshot'
	}
}
```

Output::

```
....
> Task :step4
 >> build.gradle >> Task step4 invoked for Version : 0.1-Snapshot or 1.0
```

## Before Task & After Task

1. As the name suggest these methods will be executed before & after tasks

```gradle
defaultTasks 'step4'
task step1{
	doLast{
		logger.info " >> build.gradle >> Task $name invoked"
	}
}
task step2{
	doLast{
		logger.info " >> build.gradle >> Task $name invoked"
	}
}
task step4{
	doLast{
		logger.info " >> build.gradle >> Task $name invoked for Version : $version"
	}
}
if ( Math.random() < 0.5 ) {
	step4.dependsOn step1,step2
}
project.gradle.taskGraph.whenReady { taskGraph -> 
	if ( taskGraph.hasTask ( step2 ) ) {
		project.version = '1.0'
	}else{
		project.version = '0.1-Snapshot'
	}
}
gradle.taskGraph.beforeTask { task -> 
	logger.info " >> build.gradle >> Before Task $task.name"
}
gradle.taskGraph.afterTask { task -> 
	logger.info " >> build.gradle >> After Task $task.name"
}
```

Output :

```
> Task :step1
 >> build.gradle >> Before Task step1
 >> build.gradle >> Task step1 invoked
 >> build.gradle >> After Task step1

> Task :step2
 >> build.gradle >> Before Task step2
 >> build.gradle >> Task step2 invoked
 >> build.gradle >> After Task step2

> Task :step4
 >> build.gradle >> Before Task step4
 >> build.gradle >> Task step4 invoked for Version : 1.0
 >> build.gradle >> After Task step4
```
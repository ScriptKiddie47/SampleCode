# Closure ( Complicated Topic )

1. They are kinda like lambda's in Java 8 :coffee: .
2. They are declared by a pair of curly braces.
3. Now, once we've defined a closure, we can actually assign it to a variable a bit like a value.
4. If we invoke a empty closure say `echoIt()` we get `null` back as we don't have any contents in it.
5. If we leave off the `()` we actually get a value back which is nothing but the toString() method being invoked.
6. If we don't return a value from a closure and closure are veery much like methods , they always return value in last statement. So the default value that gets returned is null. Seen in `echoIt2`. But we do see the contents of the closure got evaluated.
7. We can also pass parameters in the closure.
8. We can also type our parameters
9. Clouse does a unique thing where A closure with no declared parameters implicitly takes an untyped parameter named it.
10. Remember what was disucssed earlier that what we keep in the last line is auto returned so say write 4 at the very end of `echoIt7` clouse. It will be returned.
11. Finally in the below example we have been using `def` to define the clouse but we can be more specific and use `closure` instead.

    ```groovy
    def echoIt = { }
    println "echoIt() : " + echoIt() 
    println "echoIt   : " + echoIt   


    def echoIt2 = { 
        println "Hello from Inside the Closure"
    }
    println "echoIt2() : " + echoIt2() 
    println "echoIt2   : " + echoIt2   

    def echoIt3 = { p -> 
        println p
    }

    println "echoIt3() : " + echoIt3("Hello There Un-Typed Parameter") 
    println "echoIt3   : " + echoIt3

    def echoIt4 = { String p -> 
        println p
    }

    println "echoIt4() : " + echoIt4("Hello There Typed Parameter") 
    println "echoIt4   : " + echoIt4

    def echoIt5 = {
        println it
    }

    println "echoIt5() : " + echoIt5("Hello There Un-Typed 'it' Parameter") 
    println "echoIt5   : " + echoIt5

    def echoIt6 = { a,b ->  
        println "a : " + a
        println "b : " + b
    }

    println "echoIt6() : " + echoIt6("AA","BB") 
    println "echoIt6   : " + echoIt6

    def echoIt7 = { a,b ->  
        println "a : " + a
        println "b : " + b
        4
    }

    println "echoIt7() : " + echoIt7("AA","BB") 
    println "echoIt7   : " + echoIt7

    Closure echoIt8 = { a,b ->  
        println "a : " + a
        println "b : " + b
        88
    }

    println "echoIt8() : " + echoIt8("AA","BB") 
    println "echoIt8   : " + echoIt8
    ```

    Output :
    ```
    > Configure project :
    echoIt() : null
    echoIt   : build_a240woo1vfsc2c4lfrgs36u4s$_run_closure1@1739b88d
    Hello from Inside the Closure
    echoIt2() : null
    echoIt2   : build_a240woo1vfsc2c4lfrgs36u4s$_run_closure2@4ddddeb5
    Hello There Un-Typed Parameter
    echoIt3() : null
    echoIt3   : build_a240woo1vfsc2c4lfrgs36u4s$_run_closure3@70d1f3cc
    Hello There Typed Parameter
    echoIt4() : null
    echoIt4   : build_a240woo1vfsc2c4lfrgs36u4s$_run_closure4@73cc4b04
    Hello There Un-Typed 'it' Parameter
    echoIt5() : null
    echoIt5   : build_a240woo1vfsc2c4lfrgs36u4s$_run_closure5@10a6605c
    a : AA
    b : BB
    echoIt6() : null
    echoIt6   : build_a240woo1vfsc2c4lfrgs36u4s$_run_closure6@75ccf2ac
    a : AA
    b : BB
    echoIt7() : 4
    echoIt7   : build_a240woo1vfsc2c4lfrgs36u4s$_run_closure7@207aab8c
    a : AA
    b : BB
    echoIt8() : 88
    echoIt8   : build_a240woo1vfsc2c4lfrgs36u4s$_run_closure8@6f95d5ec

    > Task :help
    ```

## Closure Extended

### Pass Closure as Parameters
1. We can pass a `Closure` to a `method` as parameter
2. Lets create a simple method which runs the closure and multiplies its response with 2.
3. In groovy the parenthesis for a method call is optional. `oneArgMethod`
4. Not the syntax change as we modify the arguments of the newer method `twoArgMethod`.
5. When more than one arguments passed in a method and one of them is a closure. The clousure is always the last argument. The benefit here  is we don't need to wrap in parathesis.
6. Lets define a variable i say which capture the result of `oneArgMethod` and lets send a `multiline closure` as a parameter. Its quite popular , its also refered as a code block
```groovy
{...
...} // Code Block
```
7. Take a closer look at the closure iteration. The each method here accepts a closure argument.

    ```groovy
    def oneArgMethod(closure){
        closure()*2;
    }
    println "Invoked oneArgMethod : " + oneArgMethod {4}
    println "Invoked oneArgMethod : " + oneArgMethod {10}

    def twoArgMethod(factor,closure){
        closure()*factor;
    }
    println "Invoked twoArgMethod : ${twoArgMethod 2, {4}}"
    println "Invoked twoArgMethod : ${twoArgMethod 4, {10}}"

    // Multiline Clouse as parameter
    def i = oneArgMethod {
        def y = 3
        y * 2
    }

    println "I:${i}"
    for(int j in [1,2,3]){
        print j
    }
    println ""
    // Iteration powered by Closure
    [4,5,6].each{
        print it
    }
    ```

    Output :

    ```
    > Configure project :
    Invoked oneArgMethod : 8
    Invoked oneArgMethod : 20
    Invoked twoArgMethod : 8
    Invoked twoArgMethod : 40
    I:12
    123
    456
    > Task :help
    ```
## Closure Resolution

1. Its quite Simple actually. When Closure reference a `context` / `delegate` object, it works with it. If it modifies the closure reference it instead of the old one.

2. Take a deep breath and look at the below example of `delegate` & `this` usage. We are providing the closure with the Class Object context using `c.delegate = this`

    ```groovy
    class Person{
        String theName
        Integer theAge
        
        Person(theName,theAge){
            this.theName = theName
            this.theAge = theAge
        }
        def executeInside(Closure c){
            c.delegate = this
            c()
        }		
    }

    def p1 = new Person("Fred",35);
    println p1.theName

    p1.executeInside {
        println theName
    }
    ```

    Output :

    ```
    Fred
    Fred
    ```

